apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'ceylon'

defaultTasks 'compileCeylon'

repositories {
    mavenCentral()
}

ext.versions = [
      javax_validation: "1.1.0.Final",
      hibernate_validator: "5.1.3.Final",
      log4j: "1.2.17",
      guava: "18.0",
      mybatis: "3.2.8",
      mybatis_spring: "1.2.2",
      slf4j:  "1.7.10",
      spring: "4.1.4.RELEASE",
      spring_security: "3.2.5.RELEASE",
      aspectj: "1.8.5",
      tomcat: "8.0.18"]

configurations {
    fatJar.exclude group: 'commons-logging'        // replaced by org.slf4j:jcl-over-slf4j
    fatJar.exclude group: 'com.google.collections' // replaced by com.google.guava:guava
    fatJar.exclude group: 'javax.servlet'          // don't add to fat-jar
    fatJar.exclude group: 'com.google.guava'       // don't add to fat-jar
    fatJar.exclude group: 'log4j'                  // don't add to fat-jar
}

dependencies {
    fatJar \
        "javax.inject:javax.inject:1",
        "javax.validation:validation-api:${versions.javax_validation}",
        "org.hibernate:hibernate-validator:${versions.hibernate_validator}",
        "javax.el:el-api:2.2", // for hibernate-validator
        "org.glassfish.web:el-impl:2.2", // for hibernate-validator

        "log4j:log4j:${versions.log4j}",
        "org.slf4j:slf4j-api:${versions.slf4j}",
        "org.slf4j:slf4j-log4j12:${versions.slf4j}",
        "org.slf4j:jcl-over-slf4j:${versions.slf4j}",

        "org.springframework:spring-core:${versions.spring}",
        "org.springframework:spring-beans:${versions.spring}",
        "org.springframework:spring-aop:${versions.spring}",
        "org.springframework:spring-context:${versions.spring}",
        "org.springframework:spring-tx:${versions.spring}",
        "org.springframework:spring-jdbc:${versions.spring}",
        "org.springframework:spring-web:${versions.spring}",
        "org.springframework:spring-webmvc:${versions.spring}",

        "org.springframework.security:spring-security-core:${versions.spring_security}",
        "org.springframework.security:spring-security-config:${versions.spring_security}",
        "org.springframework.security:spring-security-web:${versions.spring_security}",

        "com.h2database:h2:1.4.185",
        "org.aspectj:aspectjweaver:${versions.aspectj}",

        "org.mybatis:mybatis:${versions.mybatis}",
        "org.mybatis:mybatis-spring:${versions.mybatis_spring}",
        "org.apache.tomcat:tomcat-jdbc:${versions.tomcat}"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Fat Jar for Ceylon',
                   'Implementation-Version': '1.0.0',
                   'Main-Class': 'com.example.main'
    }
    baseName = project.name + '-all'
    from { configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task ceylonDeps(dependsOn: fatJar) {
    ext.jarFile = file("build/libs/csm-petstore-all.jar")
    ext.descriptorFile = file("csmpetstoredeps.properties")
    ext.outPath = file("build/modules-deps")

    inputs.file jarFile
    inputs.file descriptorFile
    outputs.dir outPath

    doLast {
        ant."ceylon-import-jar"(
            jar: jarFile,
            out: outPath,
            descriptor: descriptorFile,
            module: "com.vasileff.csmpetstoredeps/1.0.0",
            force: true)
    }
}

compileCeylon {
    dependsOn "ceylonDeps"
    module "com.vasileff.csmpetstore"
    sourceDir file("source")
}

ceylonDoc {
    dependsOn "ceylonDeps"
    includeSource = true
    // FIXME hack to make ceylon doc not crash.
    // Currently, ceylon doc requires the output
    // directory be the same as the compile directory
    // if Java classes are present in the source
    destinationDir = compileCeylon.destinationDir
}

task war(dependsOn: compileCeylon) {
    ext.repoDeps = file("build/modules-deps")
    ext.repoJVM = file("build/modules-jvm/main")
    ext.resourceDir = file("webapp")
    ext.outPath = file("build/libs")

    inputs.dir repoDeps
    inputs.dir repoJVM
    inputs.dir resourceDir
    outputs.dir outPath

    doLast {
        ant."ceylon-war"(
              name: "csmPetstore.war",
              out: outPath,
              resourceRoot: resourceDir,
              module: "com.vasileff.csmpetstore") {

            exclude(module: "javax.servlet")
            exclude(module: "org.apache.openejb:javaee-api")
            exclude(module: "org.apache.commons.logging")
            exclude(module: "org.slf4j.api")

            rep(url: repoDeps)
            rep(url: repoJVM)
        }
    }
}

wrapper {
    jarFile 'buildSrc/wrapper/gradle-wrapper.jar'
}

